plugins {
    id 'com.android.library'
    id 'maven-publish'
}
apply from: rootProject.file('gradle/common.gradle')

android {
    defaultConfig {
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                arguments "-DANDROID_TOOLCHAIN=clang", "-DANDROID_STL=c++_shared", "-DCMAKE_VERBOSE_MAKEFILE=ON"
                cppFlags "-std=c++17", "-Wl,--gc-sections", "-fno-exceptions", "-fno-rtti", "-fvisibility=hidden"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    /*externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
        }
        version "3.10.2"
    }*/

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            jniLibs.srcDir('src/main/libs')
            jni.srcDirs = []
        }
    }
}

dependencies {
    implementation project(':kwai-linker')
    implementation project(':third-party:xhook')
    api project(':third-party:shark')
    implementation deps.kotlin.stdlib
    implementation deps.gson
    implementation deps.okio
    implementation deps.androidx.lifecycle
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.kwai.koom'
                artifactId = "java-oom"
                version = '2.0.0'
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = 'com.kwai.koom'
                artifactId = "java-oom"
                version = '2.0.0'
            }
        }
    }
}